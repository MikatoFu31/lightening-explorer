#packopt name "Lightning Explorer.exe"

#include "hspext.as"
#include "shell32.as"
/*#uselib "user32.dll"
#uselib "kernel32.dll"*/
#uselib "gdi32.dll"
#include "gdi32.as"
#include "user32.as"
#include "kernel32.as"
#include "../extensions/hotkey.hsp"
#include "../extensions/ini.as"
//#include "../extensions/hspext.as"
#include "hspmath.as"
//////////////////////////////////////////// winapiが動かねぇ
#module
/*	#func  CloseHandle  "CloseHandle"  int
	#cfunc CreateMutex  "CreateMutexA" int, int, sptr
	#cfunc GetLastError "GetLastError"
	#func  global GetModuleFileName "GetModuleFileNameA" sptr,sptr,sptr
	#func  global CreatePopupMenu "CreatePopupMenu"
	#func  global RegisterWindowMessage "RegisterWindowMessageA" sptr
	#func  global GetWindowTextLength "GetWindowTextLengthA" sptr
	#func  global GetWindowText "GetWindowTextA" sptr,sptr,sptr
	#func  global SetForegroundWindow "SetForegroundWindow" sptr
	#func  global PostMessage "PostMessageA" sptr,sptr,sptr,sptr
	#func  global InsertMenu "InsertMenu" sptr,sptr,sptr,sptr,sptr
	#func  global TrackPopupMenu "TrackPopupMenu" sptr,sptr,sptr,sptr,sptr,sptr,sptr
	#func  global DestroyMenu "DestroyMenu" sptr
	#func  global SetWindowLong "SetWindowLongA" sptr,sptr,sptr
*/
	#define PBS_MARQUEE 0x08
	#define PBM_SETMARQUEE 0x040A
	#define GWL_STYLE           (-16)
	#define WS_CHILD            0x40000000
	#define WS_VISIBLE 0x10000000
	#define PBM_SETRANGE 0x0401
	#define PBM_SETPOS 0x0402
	#define ctype MAKELPARAM(%1,%2) (%1 | %2<<16)

	// font
	#deffunc addFont str fontFileName
		AddFontResourceExA fontFileName, 0x10, 1
		sendmsg 0xffff, 0x001D, 0, 0
	return

	#deffunc removeFont str fontFileName
		RemoveFontResourceExA fontFileName, 0x10, 1
		sendmsg 0xffff, 0x001D, 0, 0
	return

	// 既起動判定
	#define ERROR_ALREADY_EXISTS    183

	; ミューテックスオブジェクトの名前の定義
	; (アプリケーション固有の名前にする必要があります)
	#define MUTEX_NAME  "HSP_lightning_explorer"

	; このアプリケーションがすでに起動されているかどうかを取得する関数
	#defcfunc AlreadyAppRunning

	if (hMutex == 0) {
	    ; 名前付きミューテックスオブジェクトの作成
	    hMutex = CreateMutex(0, 0, MUTEX_NAME)

	    ; オブジェクトがすでに作成されていたかどうかの判別
	    if (GetLastError() == ERROR_ALREADY_EXISTS) {
	        ; すでに同じ名前のオブジェクトが存在する
	        alreadyRunning = 1
	    } else {
	        ; オブジェクトが新しく作成された
	        alreadyRunning = 0
	    }
	}
	return alreadyRunning

	; クリーンアップ処理（終了時に自動実行）
	#deffunc CleanupAppRunChecker onexit
	if (hMutex != 0) {
	    ; ミューテックスオブジェクトハンドルのクローズ
	    CloseHandle hMutex
	    hMutex = 0
	}
	return

#global
	if (AlreadyAppRunning()) {
		gsel 0, -1
    	dialog "This program has already started.",1
    	end
	}
	RegisterWindowMessage "TaskbarCreated"

	onExit gosub *hideWindow

	en_font 	= "Gadugi Bold"
	univ_font = "游ゴシック"

	software_version = "1.3.0"

	// タスクトレイにアイコン表示
	screen 0
	title "Lightning Explorer"
	bgscr , 300, 80, , (ginfo_dispx - 300) / 2, (ginfo_dispy - 80) / 2
	color 197, 211, 226 : boxf
	color 140, 154, 169 : boxf 20,13,25,67
	font en_font , 25 	: color 28, 42, 57		: pos 36, 14 : mes "Lightning Explorer"
	font en_font , 13 	: color 108, 122, 137	: pos 160, 45 : mes "version" + software_version

	gosub *AddTaskbarIcon
	gosub *loadConfig
	oncmd gosub *OnTaskbarCreated, stat // "TaskbarCreated"
	oncmd gosub *OnCommand, 0x0111 		// COMMAND
	oncmd gosub *OnTrayIcon, 0x0401 	// WM_TRAYICON

	exist "playindex.rsv"
	if strsize = -1 {
		isFirstBoot = 1
		dialog "Thank you for installing Lightning Explorer.\nWhen you close this dialog it will start indexing."
		gosub *startIndexing
	}else{
		isFirstBoot = 0
	}
	gosub *hideWindow

	addhotkey 'S',3,*openLauncher // TODO: ホットキーを設定画面から変更

	stop

*AddTaskbarIcon

	// 自身のアイコン取得
	sdim FileName, 260
	GetModuleFileName hModule, varptr(FileName), 260
	ExtractIconEx varptr(FileName), 0, 0, varptr(hIconSmall), 1
	hIcon = hIconSmall(0)

	// 自身のウィンドウタイトル取得
	GetWindowTextLength hwnd
	sdim WindowTitle, stat + 1
	GetWindowText hwnd, varptr(WindowTitle), stat + 1

	// NOTIFYICONDATA 構造体
	dim IconData,22
	IconData(0) = 88
	IconData(1) = hwnd
	IconData(2) = 1
	IconData(3) = 0x0007 // NIF_MESSAGE | NIF_ICON | NIF_TIP
	IconData(4) = 0x0401 // WM_TRAYICON
	IconData(5) = hIcon
	poke IconData, 4 * 6, WindowTitle

	// タスクバーにアイコン表示
	Shell_NotifyIcon 0x00000000, varptr(IconData) // NIM_ADD

	return

*DeleteTaskbarIcon

	// NOTIFYICONDATA 構造体
	dim IconData,22
	IconData(0) = 88
	IconData(1) = hwnd
	IconData(2) = 1
	IconData(3) = 0x0002 // NIF_ICON

	// タスクバーのアイコン削除
	Shell_NotifyIcon 0x00000002, varptr(IconData) // NIM_DELETE

	return

*OnTaskbarCreated

	// タスクトレイにアイコン表示
	gosub *AddTaskbarIcon

	return

*onClose

	// タスクバーのアイコン削除
	dialog "Are you sure you want to exit?" ,2
	if	(stat = 6){
		gosub *DeleteTaskbarIcon
		end
	}
	return

*OnTrayIcon

	if (wparam == 1) {
		switch (lparam)
		case 0x0201 // WM_LBUTTONDOWN
			gosub *openLauncher
			swbreak
		case 0x0204
			SetForegroundWindow hwnd
			gosub *menu
			PostMessage hwnd1,WM_NULL,0,0
			swbreak
		swend
	}

	return

*hideWindow
	if ginfo_act=1{
		gosub *saveAndExit
	}
	gsel 0, -1
	return

*menu
		hPopupMenu=CreatePopupMenu()
		InsertMenu hPopupMenu,0,0x400,0,"Launch searcher	(Ctrl + Alt + S)"
		InsertMenu hPopupMenu,1,0x400,1,"Settings"
		InsertMenu hPopupMenu,2,0x400,2,"Refresh index"
		InsertMenu hPopupMenu,3,0x400,3,"Exit"
		TrackPopupMenu hPopupMenu,0x20,ginfo(0),ginfo(1),0,hwnd,0
		DestroyMenu hPopupMenu
	return

*onCommand
	switch wparam & 0xffff
		case 0
			gosub *openLauncher
		swbreak
		case 1
			gosub *openSettings
		swbreak
		case 2 //////////////// creating index window /////////////////////////////////////////////////////////
			dialog "Are you sure you want to make new index? \nOld one will be overwriten." ,2
			if	(stat = 6){
				gosub *startIndexing
			}
		swbreak
		case 3
			title "Lightning Explorer"
			gosub *onClose
		swbreak

		default
		swbreak
	swend
	return

*openLauncher
	exec "launcher.exe"
	return

*openSettings //////////////// setting window ////////////////////////////////////////////////////////////////
  screen 1
	gsel 1, 2
	title "Settings"
	width 300,300
	color 140, 154, 169 : boxf 20,13,25,67
	font en_font, 25 	: color 28, 42, 57		: pos 36, 14 : mes "Settings"
	font en_font, 13 	: color 108, 122, 137	: pos 50, 45 : mes "Lightning Explorer version" + software_version
	color 236, 240, 241 : boxf 0, 80, 300, 300

	font en_font, 15 	: color 28, 42, 57 : pos 20, 100 : mes "Search Engine"
	pos 140, 100 : objsize 120,24
	combox settings_searchengine, 120, "Google\nYahoo\nBing"

	pos 20,250 : button gosub "Save and Exit", *hideWindow
	pos 150,250 : button gosub "Quit", *onClose
	SetWindowLong hwnd,-16,$16CA0000
	return

*saveAndExit
	gosub *saveConfig
	gsel 1, -1
	return

*hideLogScreen
	gsel 0, -1
	return

*startIndexing
	exist "filetype.rsv"

	if strsize<=0{
		filetype="exe\ncmd\nbat\nlnk\ncom"
		bsave "filetype.rsv",filetype
	}

	exist "playpath.rsv"
	if strsize<=0{
		playpath="C:\\ProgramData\\Microsoft\\Windows\\Start Menu\\Programs\nC:\\Users\\%USERNAME%\\AppData\\Roaming\\Microsoft\\Windows\\Start Menu"
		bsave "playpath.rsv",playpath
	}

	sdim ln,4096
	sdim buf,128000

	buf = ""
	pipeexec buf, "makeindex.exe /h",1 // TODO: makeindex.exeを/hに対応させる（ログ表示ウィンドウをなくす）

	if (stat) {
		dialog "Sorry. An error has occared while creating index.",1
	}else{
		screen 0, 640, 480
		width 640, 200, 200, 100
		title "Creating index..."
		repeat
			dots = "."
			repeat fmod(cnt, 4)
				dots = dots + "."
			loop
			color 255,255,255 : boxf
			pos 246, 83 : font en_font,18 : color 0,0,0 : mes "Creating index" + dots
			pipeget ln          ; パイプの取得/更新
			if stat=0 : break   ; 実行終了ならループを抜ける
			await 100
		loop

		itemsCount = 0
		offset = 0
		result = 0
		while result != -1
			result = instr(buf,offset,"\nfound:")
			offset = offset + result + 1
			itemsCount++
			await
		wend
		itemsCount = itemsCount - 1

		screen 0, 640, 480
		title "Index creation log"
		color 140, 154, 169 : boxf 20,17,25,67
		font en_font, 25 	: color 28, 42, 57		: pos 36, 18 : mes "Index creation log"
		font en_font, 13 	: color 108, 122, 137	: pos 50, 49 : mes "Lightning Explorer version" + software_version
		font univ_font, 14 		: color 60, 74, 89		: pos 20, 85 : mes "" + itemsCount + " items were found and included to the index."
		pos 20,110 : mesbox buf,600,330,4
		pos 290, 445 : button gosub "OK", *hideLogScreen

		dialog "Index creation was completed."
		if isFirstBoot = 1{
			dialog "Let's search programs with \"Ctrl + Alt + S\" !"
			isFirstBoot = 0
		}
	}
	return

*loadConfig
		ini_path = "./settings.ini"
		ini_setpath ini_path

		exist ini_path
		if strsize = -1{
			ini_puti "lightning_explorer", "search_engine", 0
		}
    settings_searchengine = ini_geti("lightning_explorer", "search_engine")
    return

*saveConfig
	ini_puti "lightning_explorer", "search_engine", settings_searchengine
  return
